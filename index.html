<html>
<head>
    <title>WebGl Waves</title>
    <script src="lib/dat.gui.min.js" type="text/javascript"></script>
    <script src="lib/stats.min.js" type="text/javascript"></script>
    <script src="lib/lodash.min.js" type="text/javascript"></script>
    <script src="driver.js" type="text/javascript"></script>
    <style>
        * {
            margin:0;
            padding:0;
        }
        canvas {
            width:100%;
            height:100%;
        }
        .stats table * {
            color: cornflowerblue;
            font-family: Sans-Serif;
        }
    </style>
    <script id="shader-vs" type="x-shader/x-vertex">
precision mediump float;
attribute vec2 vertPosition;
void main() {
  gl_Position = vec4(vertPosition, 0.0, 1.0);
}
    </script>
    <script id="shader-fs" type="x-shader/x-fragment">
#extension GL_OES_standard_derivatives : enable

#ifdef GL_ES
precision mediump float;
#endif

uniform float waveType;
const float decayingSineWave = ${decayingSineWave};
const float decayingSquareWave = ${decayingSquareWave};
const float peakWave = ${peakWave};

uniform int mode;
const int modeNormal = ${modeNormal};
const int mode3d = ${mode3d};
const int modeTest = ${modeTest};

uniform float time;
uniform vec2 resolution;
uniform float timeOscillator;
const int wavesLength = ${wavesLength};
const int waveElements = 8;

uniform sampler2D waveSampler;

struct Wave {
    vec2 position;
    float startTime;
    float hue;
    float waveType;
    /*
    float _unused1;
    float _unused2;
    float _unused3;
    */
};

const float PI = 3.1415926535897932384626433832795;
const float PI2 = 2.0 * PI;

/** cycle smoothly from 0 to 1 and back over the specified period in seconds */
float timeCycle(float offset, float period) {
    float smooth = .5 * (1.0 + sin((time + offset) * PI2/period));
    return smooth;
}

/** convert hsv to rgb. from: http://stackoverflow.com/questions/15095909/from-rgb-to-hsv-in-opengl-glsl */
vec3 hsv2rgb(vec3 c) {
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

/** color via sin wave tamped by decaying exponential */
vec3 decayingSine(Wave wave, vec2 pos) {
    vec2 fromCenter = abs(wave.position.xy - pos / resolution.yy);
    float distance = length(fromCenter);
    float hue = wave.hue;
    float startTime = wave.startTime;

    float decayRate = -4.0;
    float scale = .5;
    float offset = 1.0;
    float stretch = 40.0 + distance;
    float cycle = (time - startTime) * 5.0;

    float decay = exp(decayRate * distance);
    float wiggle = scale * (offset + sin(distance * stretch - cycle));
    float value = wiggle * decay;
    value /= float(wavesLength / 8);
    float saturation = .7;
    vec3 rgb = hsv2rgb(vec3(hue+time/20.0, saturation, value));
    return rgb;
}

vec3 decayingSquare(Wave wave, vec2 pos) {
    vec2 fromCenter = abs(wave.position.xy - pos / resolution.yy);
    float distance = length(fromCenter);
    float hue = wave.hue;
    float startTime = wave.startTime;

    float decayRate = -4.0;
    float scale = .5;
    float offset = 1.0;
    float stretch = 40.0 + distance;
    float cycle = (time - startTime) * 5.0;

    float decay = exp(decayRate * distance);
    float wiggle = scale * (offset + (floor(sin(distance * stretch - cycle)*0.5+1.0)-0.5)*2.0);
    float value = wiggle * decay;
    value /= float(wavesLength / 8);
    float saturation = .7;
    vec3 rgb = hsv2rgb(vec3(hue+time/20.0, saturation, value));
    return rgb;
}

float peak(float x){
    return abs(2.0-mod(x/PI*2.0,4.0))-1.0;
}

vec3 decayingPeak(Wave wave, vec2 pos) {
    vec2 fromCenter = abs(wave.position.xy - pos / resolution.yy);
    float distance = length(fromCenter);
    float hue = wave.hue;
    float startTime = wave.startTime;

    float decayRate = -4.0;
    float scale = .5;
    float offset = 1.0;
    float stretch = 40.0 + distance;
    float cycle = (time - startTime) * 5.0;

    float decay = exp(decayRate * distance);
    float wiggle = scale * (offset + peak(distance * stretch - cycle));
    float value = wiggle * decay;
    value /= float(wavesLength / 8);
    float saturation = .7;
    vec3 rgb = hsv2rgb(vec3(hue+time/20.0, saturation, value));
    return rgb;
}

float distanceFromRect(vec2 pos, vec2 corner) {
    // consider the upper right quadrant of the rect.
    // (other quadrants are symmetrical)
    vec2 aPos = abs(pos);
    vec2 aCorner = abs(corner);
    vec2 toCorner = abs(aPos - aCorner);

    if (aPos.x > aCorner.x && aPos.y > aCorner.y) {
        return length(toCorner);             // point outside in corner
    } else if (aPos.x > aCorner.x) {
        return toCorner.x;                   // point outside right
    } else if (aPos.y > aCorner.y) {
        return toCorner.y;                   // point outside above
    } else {
        return min(toCorner.x, toCorner.y);  // point inside
    }
}

/** quarter sine curve, ramps smoothly from zero to one as input
    goes linearly from 0 to 1. the quarter sine ouput repeats for every integer
    rise in the input. */
float quarterSine(float x) {
    x = mod(x, 1.0);
    x *= PI;
    x -= PI / 2.0;
    float y = .5*(1.0+sin(x));
    return y;
}

vec3 spinningSquare(Wave wave) {
    float hue = wave.hue;
    float saturation = .7;
    float startTime = wave.startTime;
    vec2 center = wave.position;
    vec2 d = abs(gl_FragCoord.xy / resolution.xy - center);    // vector to our pixel
    float distance = length(d);                                // distance to our pixel
    float t = quarterSine(time - startTime);
    float maxValue = pow(1.0 - t, 5.0); // fade value as we go further from center

    float dist = distanceFromRect(d, vec2(t,t));
    float attenuation = 0.3;
    float fuzzySquare = attenuation * exp(-100.0*dist*dist);
    float value = maxValue * fuzzySquare;
    vec3 rgb = hsv2rgb(vec3(hue, saturation, value));
    return rgb;
}


/** color from one wave */
vec3 colorContrib(Wave wave, vec2 pos) {
    if (waveType == decayingSquareWave){
        return decayingSquare(wave, pos);
    } else if (waveType == peakWave){
        return decayingPeak(wave, pos);
    }
    return decayingSine(wave, pos);
}

float floor2(float value, float divisor) {
    return floor(value / divisor) * divisor;
}

/** retrieve a Wave from the data texture referenced by waveSampler */
Wave getWave(int index) {
    const float rgbaElements = 4.0;
    const float texelsPerWave = float(waveElements) / rgbaElements; 
    const float texels = float(wavesLength) * texelsPerWave;        
    const float texelSize = 1.0 / texels;                           

    float u = float(index) * texelsPerWave * texelSize;             
    vec4 v1 = texture2D(waveSampler, vec2(u, 0));
    vec4 v2 = texture2D(waveSampler, vec2(u + texelSize, 0));       

    return Wave(v1.rg, v1.b, v1.a, v2.r);
}

bool isActive(Wave wave) {
    return wave.startTime >= 0.0;
}

/** accumulate a color from all the waves for this pixel, in greyscale 3d shadow mode */
vec3 threeMode() {
    vec3 color = vec3(0.003);
    for (int i = 0; i < wavesLength; i++) {
        Wave wave = getWave(i);
        if (isActive(wave)) {
            color += vec3(length(colorContrib(wave,gl_FragCoord.xy)));
            color -= vec3(length(colorContrib(wave,gl_FragCoord.xy+vec2(10.0)/resolution.xy)));
        }
    }
    color = color*150.0;
    return color;
}

/** accumulate a color from all the waves for this pixel, in normal color mode. */
vec3 normalColorMode() {
    vec3 color = vec3(0.0, 0.0, 0.0);
    for (int i = 0; i < wavesLength; i++) {
        Wave wave = getWave(i);
        if (isActive(wave)) {
            color += colorContrib(wave,gl_FragCoord.xy);
        }
    }
    
    return color;
}


/** manual test code */
vec3 testMode() {
    const vec3 green = vec3(0.0, 1.0, 0.0);
    const vec3 red = vec3(1.0, 0.0, 0.0);
    Wave wave = getWave(0);
    if (wave.hue == .65) {
        return green;
    } else {
        return red;
    }
}

/** accumulate a color from all the waves for this pixel. */
void main( void ) {
    vec3 color;
    if (mode == mode3d) {
        color = threeMode();
    } else if (mode == modeNormal) {
        color = normalColorMode();
    } else if (mode == modeTest) {
        color = testMode();
    }
    gl_FragColor = vec4(color, 1.0);

}


    </script>
</head>

<body onload="start();">
    <div id="shaderDiv">
        <canvas id="shader"></canvas>
    </div>
</body>

</html>
