<html>
<head>
    <title>WebGl Waves</title>
    <script src="bower_components/lodash/dist/lodash.js" type="text/javascript"></script>
    <script src="lib/dat.gui.min.js" type="text/javascript"></script>
    <script src="driver.js" type="text/javascript"></script>
    <style>
        * {
            margin:0;
            padding:0;
        }
        canvas {
            width:100%;
            height:100%;
        }
        .stats table * {
            color: cornflowerblue;
            font-family: Sans-Serif;
        }
    </style>
    <script id="shader-vs" type="x-shader/x-vertex">
precision mediump float;
attribute vec2 vertPosition;
void main() {
  gl_Position = vec4(vertPosition, 0.0, 1.0);
}
    </script>
    <script id="shader-fs" type="x-shader/x-fragment">
#extension GL_OES_standard_derivatives : enable

#ifdef GL_ES
precision mediump float;
#endif

uniform int waveType;
uniform float time;
uniform int mode;
uniform vec2 resolution;
uniform float timeOscillator;
const int wavesLength = ${wavesLength};
uniform vec4 waves[wavesLength]; // [x,y,time,hue]

const float PI = 3.1415926535897932384626433832795;
const float PI2 = 2.0 * PI;

/** cycle smoothly from 0 to 1 and back over the specified period in seconds */
float timeCycle(float offset, float period) {
    float smooth = .5 * (1.0 + sin((time + offset) * PI2/period));
    return smooth;
}

/** convert hsv to rgb. from: http://stackoverflow.com/questions/15095909/from-rgb-to-hsv-in-opengl-glsl */
vec3 hsv2rgb(vec3 c) {
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

/** color via sin wave tamped by decaying exponential */
vec3 decayingSine(vec4 wave,vec2 pos) {
    vec2 fromCenter = abs(wave.xy - pos / resolution.yy);
    float distance = length(fromCenter);
    float hue = wave.w;
    float startTime = wave.z;

    float decayRate = -4.0;
    float scale = .5;
    float offset = 1.0;
    float stretch = 40.0 + distance;
    float cycle = (time - startTime) * 5.0;

    float decay = exp(decayRate * distance);
    float wiggle = scale * (offset + sin(distance * stretch - cycle)); 
    float value = wiggle * decay;
    value /= float(wavesLength / 8);
    float saturation = .7;
    vec3 rgb = hsv2rgb(vec3(hue+time/20.0, saturation, value));
    return rgb;
}
vec3 decayingSquare(vec4 wave,vec2 pos) {
    vec2 fromCenter = abs(wave.xy - pos / resolution.yy);
    float distance = length(fromCenter);
    float hue = wave.w;
    float startTime = wave.z;

    float decayRate = -4.0;
    float scale = .5;
    float offset = 1.0;
    float stretch = 40.0 + distance;
    float cycle = (time - startTime) * 5.0;

    float decay = exp(decayRate * distance);
    float wiggle = scale * (offset + (floor(sin(distance * stretch - cycle)*0.5+1.0)-0.5)*2.0); 
    float value = wiggle * decay;
    value /= float(wavesLength / 8);
    float saturation = .7;
    vec3 rgb = hsv2rgb(vec3(hue+time/20.0, saturation, value));
    return rgb;
}
float peak(float x){
    return abs(2.0-mod(x/PI*2.0,4.0))-1.0;
}
float sawtooth2(float width, float t) {
    float phase = PI * width * t;
    float value = sin(phase);
    value += sin(3.0 * phase) / 3.0;
    value += sin(5.0 * phase) / 5.0;
    value += sin(7.0 * phase) / 7.0;
    return value;
}

vec3 decayingPeak(vec4 wave,vec2 pos) {
    vec2 fromCenter = abs(wave.xy - pos / resolution.yy);
    float distance = length(fromCenter);
    float hue = wave.w;
    float startTime = wave.z;

    float decayRate = -4.0;
    float scale = .5;
    float offset = 1.0;
    float stretch = 40.0 + distance;
    float cycle = (time - startTime) * 5.0;

    float decay = exp(decayRate * distance);
    float wiggle = scale * (offset + peak(distance * stretch - cycle)); 
    float value = wiggle * decay;
    value /= float(wavesLength / 8);
    float saturation = .7;
    vec3 rgb = hsv2rgb(vec3(hue+time/20.0, saturation, value));
    return rgb;
}

/*
float receding(float startTime, float dist) {
    float cycle = timeCycle(startTime + time, 5.0);
    float scale = .5;
    float offset = 1.0;
    float value;

}
*/

float notch2(float width, float t) {
    float base = (1.3 * t / width) - 0.4;
    float value = 1.0 - pow(base, 6.0);
    return value;
}

float notch(float width, float x) {
    if (x <= width) {
        return 1.0;
    } else {
        return 0.0;
    }
}

float distanceFromRect(vec2 pos, vec2 corner) {
    // consider the upper right quadrant of the rect. 
    // (other quadrants are symmetrical)
    vec2 aPos = abs(pos);
    vec2 aCorner = abs(corner);
    vec2 toCorner = abs(aPos - aCorner);

    if (aPos.x > aCorner.x && aPos.y > aCorner.y) {
        return length(toCorner);             // point outside in corner
    } else if (aPos.x > aCorner.x) {
        return toCorner.x;                   // point outside right
    } else if (aPos.y > aCorner.y) {
        return toCorner.y;                   // point outside above
    } else {
        return min(toCorner.x, toCorner.y);  // point inside
    }
}

/** quarter sine curve, ramps smoothly from zero to one as input
    goes linearly from 0 to 1. the quarter sine ouput repeats for every integer 
    rise in the input. */
float quarterSine(float x) {
    x = mod(x, 1.0);
    x *= PI;
    x -= PI / 2.0;
    float y = .5*(1.0+sin(x));  
    return y;
}

vec3 spinningSquare(vec4 wave) {
    float hue = wave.w;
    float saturation = .7;
    float startTime = wave.z;
    vec2 center = wave.xy;
    vec2 d = abs(gl_FragCoord.xy / resolution.xy - center);    // vector to our pixel
    float distance = length(d);                                // distance to our pixel
    float t = quarterSine(time - startTime);
    float maxValue = pow(1.0 - t, 5.0); // fade value as we go further from center

    float dist = distanceFromRect(d, vec2(t,t));
    float attenuation = 0.3;
    float fuzzySquare = attenuation * exp(-100.0*dist*dist);
    float value = maxValue * fuzzySquare;
    vec3 rgb = hsv2rgb(vec3(hue, saturation, value));
    return rgb;
}


/** color from one wave */
vec3 colorContrib(vec4 wave,vec2 pos) {
    if(waveType==2){
        return decayingSquare(wave,pos);
    }
    if(waveType==3){
        return decayingPeak(wave,pos);
    }
    return decayingSine(wave,pos);
}

/** accumulate a color from all the waves for this pixel. */
void main( void ) {
    vec3 color = vec3(0.0, 0.0, 0.0);
    if(mode==2){
                color=vec3(0.003);
            }
    for (int i =0; i < wavesLength; i++) {
        if (waves[i].z > 0.0) {
            
            if(mode==2){
                color += vec3(length(colorContrib(waves[i],gl_FragCoord.xy)));
                color -= vec3(length(colorContrib(waves[i],gl_FragCoord.xy+vec2(10.0)/resolution.xy)));
            }else{
                color += colorContrib(waves[i],gl_FragCoord.xy);
                
            }
        }
    }
if(mode==2){
                color=color*150.0;
            }
    gl_FragColor = vec4( color, 1.0 );
}

    </script>
</head>

<body onload="start();">
    <div class="stats" style="position:fixed; left:10; top: 10;">
        <table>
            <tr> <td> Frame Rate:</td> 
                 <td> <span id="frameRate"> 0</span></td>
            </tr>
            <tr> <td> Frame Time:</td> 
                 <td> <span id="frameTime"> 0</span></td>
            </tr>
        </table>
    </div>
    <div id="shaderDiv">
        <canvas id="shader"></canvas>
    </div>
</body>

</html>
